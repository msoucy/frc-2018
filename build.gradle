import java.text.SimpleDateFormat

plugins {
    id "java"
    id "findbugs"
    id "pmd"
    id "edu.wpi.first.GradleRIO" version "2019.0.0-alpha-3"
}

def ROBOT_CLASS = "frc.team166.robot.Robot"

// Define my targets (RoboRIO) and artifacts (deployable files)
// This is added by GradleRIO's backing project EmbeddedTools.
deploy {
    targets {
        target("roborio", edu.wpi.first.gradlerio.frc.RoboRIO) {
            // Team number is loaded either from the .wpilib/wpilib_preferences.json
            // or from command line. If not found an exception will be thrown.
            // You can use getTeamOrDefault(team) instead of getTeamNumber if you
            // want to store a team number in this file.
            team = getTeamNumber()
        }
    }
    artifacts {
        artifact('frcJava', edu.wpi.first.gradlerio.frc.FRCJavaArtifact) {
            targets << "roborio"
            // Debug can be overridden by command line, for use with VSCode
            debug = getDebugOrDefault(false)
        }
    }
}

repositories {
    mavenCentral()
    maven {
        name "Development ChopShopLib"
        url "file://${projectDir}/../chopshoplib/build/repo"
    }
    maven {
        name "ChopShopLib"
        url "https://chopshop-166.github.io/chopshoplib/"
    }
}
// Defining my dependencies. In this case, WPILib (+ friends), CTRE Toolsuite (Talon SRX)
// and NavX.
dependencies {
    compile wpilib()
    compile ctre()
    compile navx()
    compile group: 'frc.team166', name: 'chopshoplib', version: '2019.0.0-alpha2'
}

// Setting up my Jar File. In this case, adding all libraries into the main jar ('fat jar')
// in order to make them all available at runtime. Also adding the manifest so WPILib
// knows where to look for our Robot Class.
jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest edu.wpi.first.gradlerio.GradleRIOPlugin.javaManifest(ROBOT_CLASS)
}

// Force Java 8 Compatibility mode for deployed code, in case the develoment
// system is using Java 10.
compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

def runCommand = { String... args ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine args
        standardOutput = stdout
    }
    return stdout.toString().trim()

}

def getGitHash = { -> runCommand "git", "describe", "--always" }

def getGitBranch = { -> runCommand "git", "rev-parse", "--abbrev-ref", "HEAD" }

def getGitFilesChanged = { -> runCommand("git", "diff", "--name-only", "HEAD").replace("\n", "\n") }

task versionTxt() {
    doLast {
        String resourcesDir = "$projectDir/src/main/resources"
        def logDirBase = new File(resourcesDir)
        logDirBase.mkdirs()
        new File("$resourcesDir/branch.txt").text = getGitBranch()
        new File("$resourcesDir/commit.txt").text = getGitHash()
        new File("$resourcesDir/changes.txt").text = getGitFilesChanged()
        new File("$resourcesDir/buildtime.txt").text =
                new SimpleDateFormat("dd-MM-yyyy HH:mm:ss").format(new Date())
    }
}

compileJava.dependsOn versionTxt

tasks.withType(FindBugs) {
    reports {
        xml.enabled false
        html.enabled true
        html.stylesheet resources.text.fromFile('config/xsl/fancy.xsl')
    }
}

findbugs {
    ignoreFailures = true
    excludeFilter = file("$rootProject.projectDir/config/findbugs-ignore.xml")
}

pmd {
    toolVersion = "6.6.0"
    ignoreFailures = true
    ruleSets = []
    ruleSetFiles = files("$rootProject.projectDir/config/pmd-ruleset.xml")
}

wrapper {
    gradleVersion = '4.9'
}
